! qinit_module.f90 for AVAC 4: version = 1.2
module qinit_module

    use amr_module, only: rinfinity

    implicit none
    save

    logical :: module_setup = .false.
    
    ! Type of q initialization
    integer, public :: qinit_type
    
    ! Work array
    real(kind=8), private, allocatable :: qinit(:)
    ! Optional full conservative state (h, hu, hv), used when qinit_type=5.
    real(kind=8), private, allocatable :: qinit_state(:,:)

    ! Geometry
    real(kind=8) :: x_low_qinit
    real(kind=8) :: y_low_qinit
    real(kind=8) :: t_low_qinit
    real(kind=8) :: x_hi_qinit
    real(kind=8) :: y_hi_qinit
    real(kind=8) :: t_hi_qinit
    real(kind=8) :: dx_qinit
    real(kind=8) :: dy_qinit
    
    integer, private :: mx_qinit
    integer, private :: my_qinit

    ! for initializing using force_dry to indicate dry regions below sealevel:

    integer :: mx_fdry, my_fdry
    real(kind=8) :: xlow_fdry, ylow_fdry, xhi_fdry, yhi_fdry, dx_fdry, dy_fdry
    integer(kind=1), allocatable :: force_dry(:,:)
    logical :: use_force_dry
    real(kind=8) :: tend_force_dry  ! always use mask up to this time

    logical :: variable_eta_init

    ! to initialize using different initial eta values in different regions:
    integer :: etain_mx, etain_my
    real(kind=8) :: etain_dx, etain_dy
    real(kind=8), allocatable :: etain_x(:), etain_y(:), etain_eta(:,:)


contains

    subroutine set_qinit(fname)
    
        use geoclaw_module, only: GEO_PARM_UNIT

    
        implicit none
        
        ! Subroutine arguments
        character(len=*), optional, intent(in) :: fname
        
        ! File handling
        integer, parameter :: unit = 7
        character(len=150) :: qinit_fname
        character(len=150) :: fname_force_dry

        integer :: num_force_dry
        
        if (.not.module_setup) then
            write(GEO_PARM_UNIT,*) ' '
            write(GEO_PARM_UNIT,*) '--------------------------------------------'
            write(GEO_PARM_UNIT,*) 'SETQINIT:'
            write(GEO_PARM_UNIT,*) '-------------'
            
            ! Open the data file
            if (present(fname)) then
                call opendatafile(unit,fname)
            else
                call opendatafile(unit,"qinit.data")
            endif
            
            read(unit,"(i1)") qinit_type
            if (qinit_type == 0) then
                ! No perturbation specified
                write(GEO_PARM_UNIT,*)  '  qinit_type = 0, no perturbation'
                print *,'  qinit_type = 0, no perturbation'
            else
                read(unit,*) qinit_fname
                write(GEO_PARM_UNIT,*)  qinit_fname
            
                call read_qinit(qinit_fname)
            endif


            ! If variable_eta_init then function set_eta_init is called
            ! to set initial eta when interpolating onto newly refined patches
            read(unit,*) variable_eta_init

            
            read(unit,*) num_force_dry
            use_force_dry = (num_force_dry > 0)

            if (num_force_dry > 1) then
                write(6,*) '*** num_force_dry > 1 not yet implemented'
                stop
                endif

            if (use_force_dry) then
                read(unit,*) fname_force_dry
                read(unit,*) tend_force_dry
                call read_force_dry(trim(fname_force_dry))
                endif

            module_setup = .true.
        end if
    
    end subroutine set_qinit


    subroutine add_perturbation(meqn,mbc,mx,my,xlow_patch,ylow_patch,dx,dy,q,maux,aux)
    
        use geoclaw_module, only: sea_level, coordinate_system
        use amr_module, only: mcapa
    
        implicit none
    
        ! Subroutine arguments
        integer, intent(in) :: meqn,mbc,mx,my,maux
        real(kind=8), intent(in) :: xlow_patch,ylow_patch,dx,dy
        real(kind=8), intent(inout) :: q(meqn,1-mbc:mx+mbc,1-mbc:my+mbc)
        real(kind=8), intent(inout) :: aux(maux,1-mbc:mx+mbc,1-mbc:my+mbc)
        
        ! Local
        integer :: i,j,m
        real(kind=8) :: xim,x,xip,yjm,y,yjp,dq
        real(kind=8) :: x_source_low,x_source_high
        real(kind=8) :: y_source_low,y_source_high
        
        if (qinit_type > 0) then
            do i=1-mbc,mx+mbc
                x = xlow_patch + (i-0.5d0)*dx
                xim = x - 0.5d0*dx
                xip = x + 0.5d0*dx
                do j=1-mbc,my+mbc
                    y = ylow_patch + (j-0.5d0)*dy
                    yjm = y - 0.5d0*dy
                    yjp = y + 0.5d0*dy

                    ! init.xyz values are raster-cell values located at cell
                    ! centres, not nodal samples.  Conservatively average the
                    ! piecewise-constant source cells over this solver cell.
                    ! This is exact for aligned equal-resolution grids and
                    ! avoids smearing a release edge into its dry neighbour.
                    x_source_low  = x_low_qinit - 0.5d0*dx_qinit
                    x_source_high = x_hi_qinit  + 0.5d0*dx_qinit
                    y_source_low  = y_low_qinit - 0.5d0*dy_qinit
                    y_source_high = y_hi_qinit  + 0.5d0*dy_qinit
                    if ((xip > x_source_low).and.(xim < x_source_high).and. &
                        (yjp > y_source_low).and.(yjm < y_source_high)) then

                        if (qinit_type == 5) then
                            do m=1,min(3,meqn)
                                dq = qinit_cell_average(xim,xip,yjm,yjp,m)
                                if (coordinate_system == 2) then
                                    dq = dq / aux(mcapa,i,j)
                                endif
                                q(m,i,j) = dq
                            end do
                            cycle
                        end if

                        dq = qinit_cell_average(xim,xip,yjm,yjp,1)
                        if (coordinate_system == 2) then
                            dq = dq / aux(mcapa,i,j)
                        endif
! modified Feb 2025
!                        if (qinit_type < 4) then 
!                            if (aux(1,i,j) <= sea_level) then
!                                q(qinit_type,i,j) = q(qinit_type,i,j) + dq
!                            endif
!                        else if (qinit_type == 4) then
!                            q(1,i,j) = max(dq-aux(1,i,j),0.d0)
!                        endif
                        if (qinit_type >= 1 .and. qinit_type <= 3) then
                            q(qinit_type,i,j) = dq
                        else if (qinit_type == 4) then
                            q(1,i,j) = max(dq-aux(1,i,j),0.d0)
                        end if
                    endif
                enddo
            enddo
        endif
        
    end subroutine add_perturbation


    function qinit_cell_average(xim,xip,yjm,yjp,component) result(q_average)
        ! Conservative overlap average of a cell-centred, piecewise-constant
        ! qinit raster onto one computational cell.  The file is ordered from
        ! the north-west corner to the south-east corner.

        implicit none

        real(kind=8), intent(in) :: xim,xip,yjm,yjp
        integer, intent(in) :: component
        real(kind=8) :: q_average
        real(kind=8) :: x_domain_low,y_domain_low
        real(kind=8) :: source_x_left,source_x_right
        real(kind=8) :: source_y_bottom,source_y_top
        real(kind=8) :: overlap_x,overlap_y,weighted_sum,target_area
        integer :: i,k,j_north,index_q
        integer :: i_first,i_last,k_first,k_last

        q_average = 0.d0
        weighted_sum = 0.d0
        target_area = (xip-xim)*(yjp-yjm)
        if (target_area <= 0.d0) return

        x_domain_low = x_low_qinit - 0.5d0*dx_qinit
        y_domain_low = y_low_qinit - 0.5d0*dy_qinit

        i_first = max(1, floor((xim-x_domain_low)/dx_qinit) + 1)
        i_last  = min(mx_qinit, ceiling((xip-x_domain_low)/dx_qinit))
        k_first = max(1, floor((yjm-y_domain_low)/dy_qinit) + 1)
        k_last  = min(my_qinit, ceiling((yjp-y_domain_low)/dy_qinit))

        do k = k_first,k_last
            source_y_bottom = y_domain_low + (k-1)*dy_qinit
            source_y_top    = source_y_bottom + dy_qinit
            overlap_y = max(0.d0,min(yjp,source_y_top) - &
                                  max(yjm,source_y_bottom))
            if (overlap_y <= 0.d0) cycle

            ! qinit rows are stored north-to-south, whereas k is counted
            ! south-to-north for straightforward overlap geometry.
            j_north = my_qinit-k+1
            do i = i_first,i_last
                source_x_left  = x_domain_low + (i-1)*dx_qinit
                source_x_right = source_x_left + dx_qinit
                overlap_x = max(0.d0,min(xip,source_x_right) - &
                                      max(xim,source_x_left))
                if (overlap_x <= 0.d0) cycle
                index_q = (j_north-1)*mx_qinit+i
                if (qinit_type == 5) then
                    weighted_sum = weighted_sum + &
                        qinit_state(component,index_q)*overlap_x*overlap_y
                else
                    weighted_sum = weighted_sum + qinit(index_q)*overlap_x*overlap_y
                end if
            enddo
        enddo

        q_average = weighted_sum/target_area
    end function qinit_cell_average

        
    subroutine read_qinit(fname)

        use geoclaw_module, only: GEO_PARM_UNIT

        implicit none

        character(len=150), intent(in) :: fname
        integer, parameter :: unit = 19
        integer :: status
        character(len=8) :: magic

        magic = ''

        print *,'  '
        print *,'Reading qinit data from file  ', fname
        print *,'  '

        write(GEO_PARM_UNIT,*) '  '
        write(GEO_PARM_UNIT,*) 'Reading qinit data from'
        write(GEO_PARM_UNIT,*) fname
        write(GEO_PARM_UNIT,*) '  '

        ! Probe the first eight bytes.  New plugin-prepared cases use a
        ! versioned binary raster; legacy standalone cases remain formatted
        ! x/y/value text and are handled by the original reader below.
        open(unit=unit, file=fname, iostat=status, status="old", &
             access="stream",form="unformatted",action="read")
        if (status == 0) then
            read(unit,iostat=status) magic
            close(unit)
        endif
        if ((status == 0).and.(magic == 'AVACQIN1')) then
            call read_qinit_binary(fname)
        else
            call read_qinit_text(fname)
        endif

    end subroutine read_qinit


    subroutine read_qinit_binary(fname)

        implicit none

        character(len=150), intent(in) :: fname
        integer, parameter :: unit = 19
        integer :: status,components,reserved,num_points
        integer(kind=8) :: mx_file,my_file,num_points_8
        real(kind=8) :: x_low_file,y_hi_file,dx_file,dy_file
        character(len=8) :: magic

        open(unit=unit, file=fname, iostat=status, status="old", &
             access="stream",form="unformatted",action="read", &
             convert="little_endian")
        if (status /= 0) then
            print *,"Error opening binary qinit file ",fname
            stop
        endif

        read(unit,iostat=status) magic,mx_file,my_file,components,reserved, &
                                x_low_file,y_hi_file,dx_file,dy_file
        if ((status /= 0).or.(magic /= 'AVACQIN1')) then
            print *,"ERROR: Invalid AVAC binary qinit header in ",fname
            stop
        endif
        if ((mx_file < 2).or.(my_file < 2).or. &
            (mx_file > int(huge(mx_qinit),kind=8)).or. &
            (my_file > int(huge(my_qinit),kind=8))) then
            print *,"ERROR: Invalid AVAC binary qinit dimensions"
            stop
        endif
        if (my_file > int(huge(num_points),kind=8)/mx_file) then
            print *,"ERROR: AVAC binary qinit raster is too large"
            stop
        endif

        mx_qinit = int(mx_file)
        my_qinit = int(my_file)
        num_points_8 = mx_file*my_file
        num_points = int(num_points_8)
        x_low_qinit = x_low_file
        y_hi_qinit = y_hi_file
        dx_qinit = dx_file
        dy_qinit = dy_file
        x_hi_qinit = x_low_qinit + (mx_qinit-1)*dx_qinit
        y_low_qinit = y_hi_qinit - (my_qinit-1)*dy_qinit

        if ((dx_qinit <= 0.d0).or.(dy_qinit <= 0.d0)) then
            print *,"ERROR: Invalid AVAC binary qinit cell spacing"
            stop
        endif
        if (qinit_type == 5) then
            if (components /= 3) then
                print *,"ERROR: qinit_type 5 requires three binary components"
                stop
            endif
            allocate(qinit_state(3,num_points))
            read(unit,iostat=status) qinit_state
        else
            if (components /= 1) then
                print *,"ERROR: Scalar qinit requires one binary component"
                stop
            endif
            allocate(qinit(num_points))
            read(unit,iostat=status) qinit
        endif
        close(unit)
        if (status /= 0) then
            print *,"ERROR: Incomplete AVAC binary qinit payload in ",fname
            stop
        endif
        print *,"Loaded binary qinit grid ",mx_qinit," x ",my_qinit

    end subroutine read_qinit_binary


    subroutine read_qinit_text(fname)
        ! Legacy x,y,z values, one per line from NW to SE.  This path remains
        ! available for existing projects and standalone AVAC workflows.

        implicit none

        character(len=150), intent(in) :: fname
        integer, parameter :: unit = 19
        integer :: i,num_points,status
        double precision :: x,y

        open(unit=unit, file=fname, iostat=status, status="old", &
             form='formatted',action="read")
        if ( status /= 0 ) then
            print *,"Error opening file", fname
            stop
        endif
        
        ! Initialize counters
        num_points = 0
        mx_qinit = 0
        
        ! Read in first values, determines x_low and y_hi
        read(unit,*) x_low_qinit,y_hi_qinit
        num_points = num_points + 1
        mx_qinit = mx_qinit + 1
        
        ! Sweep through first row figuring out mx
        y = y_hi_qinit
        do while (y_hi_qinit == y)
            read(unit,*) x,y
            num_points = num_points + 1
            mx_qinit = mx_qinit + 1
        enddo
        ! We over count by one in the above loop
        mx_qinit = mx_qinit - 1
        
        ! Continue to count the rest of the lines
        do
            read(unit,*,iostat=status) x,y
            if (status /= 0) exit
            num_points = num_points + 1
        enddo
        if (status > 0) then
            print *,"ERROR:  Error reading qinit file ",fname
            stop
        endif
        
        ! Extract rest of geometry
        x_hi_qinit = x
        y_low_qinit = y
        my_qinit = num_points / mx_qinit
        dx_qinit = (x_hi_qinit - x_low_qinit) / (mx_qinit-1)
        dy_qinit = (y_hi_qinit - y_low_qinit) / (my_qinit-1)
        
        rewind(unit)
        if (qinit_type == 5) then
            allocate(qinit_state(3,num_points))
            do i=1,num_points
                read(unit,*) x,y,qinit_state(1,i),qinit_state(2,i), &
                              qinit_state(3,i)
            enddo
        else
            allocate(qinit(num_points))
            do i=1,num_points
                read(unit,*) x,y,qinit(i)
            enddo
        end if
        close(unit)

    end subroutine read_qinit_text

    subroutine read_force_dry(fname)

        use utility_module, only: parse_values
        character(len=*), intent(in) :: fname
        integer :: iunit,i,j,n
        real(kind=8) :: values(16), nodata_value
        character(len=80) :: str

        iunit = 8
    
        open(unit=iunit,file=fname,status='old',form='formatted')
        !read(iunit,*) tend_force_dry
        !write(6,*) 'tend_force_dry = ',tend_force_dry
        read(iunit,*) mx_fdry
        read(iunit,*) my_fdry
        read(iunit,*) xlow_fdry
        read(iunit,*) ylow_fdry

        read(iunit,'(a)') str
        call parse_values(str, n, values)
        dx_fdry = values(1)
        if (n == 2) then
            dy_fdry = values(2)
          else
            dy_fdry = dx_fdry
          endif

        read(iunit,*) nodata_value
        allocate(force_dry(mx_fdry,my_fdry))

        xhi_fdry = xlow_fdry + mx_fdry*dx_fdry
        yhi_fdry = ylow_fdry + my_fdry*dy_fdry
        write(6,*) '+++ xlow_fdry, xhi_fdry: ',xlow_fdry, xhi_fdry
        write(6,*) '+++ ylow_fdry, yhi_fdry: ',ylow_fdry, yhi_fdry

        do j=1,my_fdry
            read(iunit, *) (force_dry(i,j), i=1,mx_fdry)
            enddo
    
        close(iunit)
        return
    end subroutine read_force_dry

    
    subroutine read_eta_init(file_name)
        ! To read in file specifying different eta value in at different
        ! locations, then used in qinit function.
        ! Uses etain module variables.
        
        implicit none

        ! Input arguments
        character(len=*), intent(in), optional :: file_name
        
        ! local 
        integer, parameter :: iunit = 7
        integer :: i,j
        real(kind=8) :: nodata_value, xllower, yllower

        if (present(file_name)) then
            open(unit=iunit, file=file_name, status='unknown',&
                      form='formatted')
        else
            open(unit=iunit, file='eta_init.data', status='unknown',&
                      form='formatted')
        endif
        
        read(iunit,*) etain_mx
        !write(6,*) '+++ etain_mx = ',etain_mx
        read(iunit,*) etain_my
        !write(6,*) '+++ etain_my = ',etain_my
        read(iunit,*) xllower
        read(iunit,*) yllower
        read(iunit,*) etain_dx
        etain_dy = etain_dx
        !read(iunit,*) etain_dy
        read(iunit,*) nodata_value
        
        allocate(etain_x(etain_mx), etain_y(etain_my))
        allocate(etain_eta(etain_mx, etain_my))
        
        do i=1,etain_mx
            etain_x(i) = xllower + etain_dx*(i-1)
            enddo
            
        do j=1,etain_my
            etain_y(j) = yllower + etain_dy*(etain_my-j+1)
            read(iunit,*) (etain_eta(i,j),i=1,etain_mx)
            enddo

        
        close(unit=iunit)
    end subroutine read_eta_init

end module qinit_module
